version 1.0;

ns junos = "http://xml.juniper.net/junos/*/junos";
ns xnm = "http://xml.juniper.net/xnm/1.1/xnm";
ns jcs = "http://xml.juniper.net/junos/commit-scripts/1.0";
ns ext = "http://xmlsoft.org/XSLT/namespace";

import "../import/junos.xsl";

match / {
<op-script-results> {

    var $usage = "This script disables the interface specified by the user." _  
                         "The script modifies the candidate configuration to disable " _
                         "the interface and commits the configuration to activate it.";
    var $temp = jcs:output($usage);

    var $interface = jcs:get-input("Enter interface to disable: ");

    var $config-changes = {
        <configuration> {
            <interfaces> {
                <interface> {
                    <name> $interface;
                    <disable>;
                }
            }
        }
    }

    var $load-action = "merge";

    var $options := {
       <commit-options> {
           <log> "disabling interface " _ $interface;
        }
    }

    var $conn_handle = jcs:open();

    var $results := { 
      call jcs:load-configuration( $action=$load-action, $commit-options=$options, 
                $configuration=$config-changes, $connection=$conn_handle); 
    }

    var $close-results = jcs:close($conn_handle);
}
}
